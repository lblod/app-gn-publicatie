PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX mandaat: <http://data.vlaanderen.be/ns/mandaat#>
PREFIX besluit: <http://data.vlaanderen.be/ns/besluit#>
PREFIX mu: <http://mu.semte.ch/vocabularies/core/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX foaf: <http://xmlns.com/foaf/0.1/>

INSERT {
    GRAPH <http://mu.semte.ch/graphs/public> {
        ?session foaf:page ?redirectUrl.
    }
}
WHERE {
    ?session rdf:type besluit:Zitting ;
        mu:uuid ?zittingUuid ;
        (besluit:isGehoudenDoor/mandaat:isTijdspecialisatieVan) ?administrativeUnit .
    ?administrativeUnit besluit:bestuurt ?bestuurseenheid .
    ?bestuurseenheid skos:prefLabel ?administrativeUnitName ;
        (besluit:classificatie/skos:prefLabel) ?administrativeUnitTypeName .
    BIND(
        CONCAT("/", ?administrativeUnitName, "/", ?administrativeUnitTypeName, "/zittingen/", ?zittingUuid)
        AS ?redirectUrl
    )
}


